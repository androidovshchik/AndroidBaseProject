apply plugin: 'com.android.library'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'com.github.dcendents.android-maven'

apply plugin: 'org.jetbrains.dokka-android'

group = 'com.github.androidovshchik'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 25
        versionName "2.2.2"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:28.0.0-beta01'
    implementation 'com.android.support:support-v4:28.0.0-beta01'
    implementation 'com.android.support:design:28.0.0-beta01'
    implementation 'com.android.support:recyclerview-v7:28.0.0-beta01'
    implementation 'com.android.support:cardview-v7:28.0.0-beta01'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.12'
    implementation 'com.jakewharton.timber:timber:4.7.0'
    implementation 'com.snatik:storage:2.1.0'
    implementation 'com.squareup.sqlbrite3:sqlbrite:3.2.0'
    implementation 'android.arch.persistence:db:1.1.1'
    implementation 'android.arch.persistence:db-framework:1.1.1'
    implementation 'ch.acra:acra:4.9.2'
    implementation 'gun0912.ted:tedpermission-rx2:2.2.0'
    implementation 'com.facebook.stetho:stetho:1.5.0'
    implementation 'com.facebook.stetho:stetho-urlconnection:1.5.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion '28.0.0-beta01'
            }
        }
    }
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/kdoc"
}

task packageSources(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.release.java.srcDirs
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaAndroidTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

task javadocJar(type: Jar, dependsOn: 'dokkaJavadoc') {
    classifier = 'javadoc'
    from fileTree(new File(project.buildDir, 'javadoc'))
}

task dokkaJar(type: Jar, dependsOn: 'dokka') {
    classifier = 'kdoc'
    from fileTree(new File(project.buildDir, 'kdoc'))
}

artifacts {
    archives packageSources
    archives javadocJar
    archives dokkaJar
}